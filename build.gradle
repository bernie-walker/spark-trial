plugins {
    id 'scala'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

def gitSha = 'git rev-parse --short HEAD'.execute().text.trim()

version = "$majorVersion.$gitSha"

sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

dependencies {
    implementation "org.scala-lang:scala-library:$scalaVersion"

    compileOnly "org.apache.spark:spark-sql_$artifactScalaVersion:$sparkVersion"
}

shadowJar {
    zip64 true
    archiveClassifier = ''
}

def sparkApps = ['SimpleApp', 'FlightDataApp']
def jarPath = "${shadowJar.destinationDirectory.get()}/${shadowJar.archiveFileName.get()}"

sparkApps.each { app ->
    def appClass = "com.berniewalker.$app"
    tasks.register("sparkSubmit$app", Exec) {
        description = "Submits the spark application $appClass"
        dependsOn 'shadowJar'
        executable 'spark-submit'
        args '--class', appClass, jarPath
    }
}
